// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Do we need to perform action?
    Or8Way(in[0]=load, in[1]=inc, in[2]=reset, in[3..7]=false, out=performAction);

    // Get incremented amount
    Inc16(in=prevCount, out=newCount);
    // Choose incremented amount OR current amount
    Mux16(a=newCount, b=in, sel=load, out=afterIncrement);

    // Choose incremented amount OR reset
    Mux16(a=afterIncrement, b=false, sel=reset, out=afterReset);

    Register(in=afterReset, load=performAction, out=prevCount, out=out);
}
